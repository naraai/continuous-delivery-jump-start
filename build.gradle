buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.2'
    }
}

apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'com.bmuschko.tomcat'
apply plugin: 'findbugs'
apply plugin: 'jacoco'

group = 'org.gradle.training'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

sourceSets {
  integrationTest {
    java.srcDir "src/integTest/java"
    resources.srcDir "src/integTest/resources"
	compileClasspath += sourceSets.main.output + configurations.testRuntime
	runtimeClasspath += output + compileClasspath
  }
}

dependencies {
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile 'org.springframework:spring-webmvc:4.1.1.RELEASE'
    compile 'org.apache.httpcomponents:httpclient:4.3.6'
    compile 'commons-io:commons-io:2.4'
    compile 'com.googlecode.json-simple:json-simple:1.1.1'
    providedCompile 'javax.servlet:servlet-api:2.5'
    runtime 'jstl:jstl:1.2'
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.10.8'
    integrationTestCompile 'org.springframework:spring-test:4.1.1.RELEASE'
	
    def tomcatVersion = '7.0.62'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
           "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

tomcat {
    contextPath = 'sample-app'
}

test {
  reports {
    html.destination = "$html.destination/unit"
    junitXml.destination = "$junitXml.destination/unit"
  }
  maxParallelForks=2
  
  testLogging {
    events 'skipped'
  }
}

task integrationTest(type: Test) {
  group 'verification'
  description 'Runs the integration tests'
  mustRunAfter test
  testClassesDir = sourceSets.integrationTest.output.classesDir
  classpath = sourceSets.integrationTest.runtimeClasspath
  reports {
    html.destination = file("$reports.html.destination/integration")
    junitXml.destination = file("$reports.junitXml.destination/integration")
  }
}

check.dependsOn integrationTest

findbugs {
  ignoreFailures = true
  toolVersion = "3.0.1"
}

tasks.withType(FindBugs) {
 reports {
   xml.enabled = false
   html.enabled = true   
 }
}

// lab 9

tasks.withType(Test) {
    jacoco {
        enabled = true //project.hasProperty('codeCoverageEnabled') ? Boolean.valueOf(project.getProperty('codeCoverageEnabled')) : false
    }
}

ext {
    jacocoReportsBaseDir = file("$buildDir/reports/jacoco")
    jacocoReportsExecDir = file("$jacocoReportsBaseDir/exec")
    jacocoReportsHtmlDir = file("$jacocoReportsBaseDir/html")
}

test.jacoco.destinationFile = file("$jacocoReportsExecDir/unit.exec")
integrationTest.jacoco.destinationFile = file("$jacocoReportsExecDir/integration.exec")

jacocoTestReport {
    reports {
        html.destination file("$jacocoReportsHtmlDir/unit")
    }
}

task jacocoIntegrationTestReport(type: JacocoReport) {
    executionData integrationTest
    sourceSets sourceSets.main

    reports {
        html.destination file("$jacocoReportsHtmlDir/integration")
    }
}

// lab 9

war.baseName = 'myapp'

// lab 10

version = new ProjectVersion(1, 4, new Date())

@groovy.transform.TupleConstructor
class ProjectVersion {
final Integer major
final Integer minor
final def build
@Override
String toString() {
String fullVersion = "$major.$minor"
if(build) {
fullVersion += ".${build.format('yyyyMMddHHmmss')}"
}
fullVersion
}
}

// lab 12
apply from: "gradle/deployment.gradle"

